//import proguard.gradle.ProGuardTask

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.guardsquare:proguard-gradle:7.6.1'
    }
}

plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.0'
}

group = 'com.erosmari'
version = '1.1.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        url = "https://repo.extendedclip.com/releases/"
    }
    maven {
        url = "https://maven.enginehub.org/repo/"
    }
    maven {
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        url = "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT")
    //compileOnly("org.spigotmc:spigot-api:1.21.4-R0.1-SNAPSHOT")
    implementation("net.kyori:adventure-platform-bukkit:4.3.4")
    implementation("net.kyori:adventure-text-minimessage:4.20.0")
    compileOnly("me.clip:placeholderapi:2.11.6")
    implementation("com.zaxxer:HikariCP:5.0.1")
    compileOnly files("APIs/CoreProtect-23.2-RC3.jar")
    implementation(platform("com.intellectualsites.bom:bom-newest:1.52"))
    compileOnly("com.fastasyncworldedit:FastAsyncWorldEdit-Core")
    compileOnly("com.fastasyncworldedit:FastAsyncWorldEdit-Bukkit") {
        transitive = false
    }
    implementation("org.bstats:bstats-bukkit:3.0.2")
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(21))
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(21)
    //options.compilerArgs += ['-Xlint:deprecation']
}

processResources {
    filesMatching('plugin.yml') {
        expand 'version': version
    }
}

tasks.shadowJar {
    archiveClassifier.set('')
    minimize()
    relocate 'org.bstats', 'com.erosmari.vitamin.shaded.bstats'
}

tasks.build {
    dependsOn tasks.shadowJar
}